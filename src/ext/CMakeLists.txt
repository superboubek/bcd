
SET(BUILD_TESTING OFF CACHE BOOL "" FORCE)

MESSAGE(STATUS "Entering subdirectory 'eigen'")
ADD_SUBDIRECTORY(eigen)

SET(BUILD_SHARED_LIBS OFF CACHE BOOL "" FORCE)


if(POLICY CMP0026)
	cmake_policy(PUSH)
	cmake_policy(SET CMP0026 OLD) # to avoid some cmake warnings
	SET(saved_policy_default_CMP0026 "${CMAKE_POLICY_DEFAULT_CMP0026}")
	SET(CMAKE_POLICY_DEFAULT_CMP0026 OLD)
	MESSAGE(STATUS "Policy CMP0026 set to OLD for IlmBase to avoid warnings")
endif()

MESSAGE(STATUS "Entering subdirectory 'IlmBase'")
ADD_SUBDIRECTORY(openexr/IlmBase)

if(POLICY CMP0026)
	SET(CMAKE_POLICY_DEFAULT_CMP0026 ${saved_policy_default_CMP0026})
	cmake_policy(POP)
endif()




TARGET_INCLUDE_DIRECTORIES(Half INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/openexr/IlmBase/Half)
TARGET_INCLUDE_DIRECTORIES(IlmThread INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/openexr/IlmBase/IlmThread)
TARGET_INCLUDE_DIRECTORIES(IlmThread INTERFACE ${CMAKE_CURRENT_BINARY_DIR}/openexr/IlmBase/config)
TARGET_INCLUDE_DIRECTORIES(Imath INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/openexr/IlmBase/Imath)
TARGET_INCLUDE_DIRECTORIES(Iex INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/openexr/IlmBase/Iex)
ADD_LIBRARY(Iex-2_2 ALIAS Iex)
ADD_LIBRARY(IlmThread-2_2 ALIAS IlmThread)
ADD_LIBRARY(Imath-2_2 ALIAS Imath)

MESSAGE(STATUS "Entering subdirectory 'OpenEXR'")
ADD_SUBDIRECTORY(openexr/OpenEXR)

TARGET_INCLUDE_DIRECTORIES(dwaLookups PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/openexr/IlmBase/Imath)
TARGET_INCLUDE_DIRECTORIES(IlmImf INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/openexr/OpenEXR/IlmImf)
TARGET_INCLUDE_DIRECTORIES(IlmImf INTERFACE ${CMAKE_CURRENT_BINARY_DIR}/openexr/OpenEXR/config)

ADD_LIBRARY(OpenExr INTERFACE)
TARGET_LINK_LIBRARIES(OpenExr INTERFACE Half Iex Imath IlmImf IlmThread)


IF(BCD_BUILD_GUI)
	MESSAGE(STATUS "Entering subdirectory 'nanogui'")
	SET(NANOGUI_BUILD_EXAMPLE OFF)
	SET(NANOGUI_BUILD_PYTHON OFF)
	SET(NANOGUI_BUILD_SHARED OFF)
	SET(NANOGUI_INSTALL OFF)
	ADD_SUBDIRECTORY(nanogui)

	ADD_LIBRARY(NanoguiTarget INTERFACE)

#	message(AUTHOR_WARNING "in ext: NANOGUI_EXTRA_LIBS = '${NANOGUI_EXTRA_LIBS}'")
#	message(AUTHOR_WARNING "in ext: NANOGUI_EXTRA_INCS = '${NANOGUI_EXTRA_INCS}'")
#	message(AUTHOR_WARNING "in ext: NANOGUI_EXTRA_DEFS = '${NANOGUI_EXTRA_DEFS}'")

	TARGET_LINK_LIBRARIES(NanoguiTarget INTERFACE nanogui)
	TARGET_LINK_LIBRARIES(NanoguiTarget INTERFACE ${NANOGUI_EXTRA_LIBS})

	TARGET_INCLUDE_DIRECTORIES(NanoguiTarget INTERFACE ${NANOGUI_EXTRA_INCS})
	TARGET_INCLUDE_DIRECTORIES(NanoguiTarget INTERFACE nanogui/include)

	TARGET_COMPILE_DEFINITIONS(NanoguiTarget INTERFACE ${NANOGUI_EXTRA_DEFS})

ELSE(BCD_BUILD_GUI)
	MESSAGE(STATUS "skipping building of NanoGUI")
ENDIF(BCD_BUILD_GUI)

ADD_LIBRARY(Json INTERFACE)
TARGET_INCLUDE_DIRECTORIES(Json INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}/json/src)

